#!/usr/bin/env perl6
use lib 'blib/lib';
use Steroids;

constant TILESIZE = 64;
class Mario is Steroids::Entity {
    has $.image;

    has @!edges = [], [], [], [];

    method move($dx, $dy) {
        $.x += $dx;
        $.y += $dy;
        self.update-edges;
    }

    method update-edges {
        @!edges[0][0] = ($.x);           @!edges[0][1] = ($.y);
        @!edges[1][0] = ($.x + $.w - 1); @!edges[1][1] = ($.y);
        @!edges[2][0] = ($.x);           @!edges[2][1] = ($.y + $.h - 1);
        @!edges[3][0] = ($.x + $.w - 1); @!edges[3][1] = ($.y + $.h - 1);
    }

    method report {
        print "Tiles:";
        for @!edges -> $e {
            print " (" ~ ($e[0] div TILESIZE) ~ ", " ~ ($e[1] div TILESIZE) ~ "),"
        }
        say '';
    }
}

constant SKY = 0;
constant GRASS = 1;
constant BRICK = 2;

my %tile-mapping =
    s => SKY,
    G => GRASS,
    B => BRICK;
 
class Level {
    has @.sprites;
    has @.reachable;
}

class Main is Steroids::State {
    has $!mario;
    has $!level;

    method load_level(Str $filename) {
        $!level = Level.new;
        my $i = 0;
        for lines($filename.IO) -> $line {
            my $j = 0;
            for $line.comb -> $tile {
                $!level.sprites[$j][$i] = %tile-mapping{$tile};
                $!level.reachable[$j][$i] = so $tile ~~ /<[a..z]>/;
                $j++;
            }
            $i++;
        }
    }

    method draw_level() {
        my $x = 0;
        for ^$!level.sprites.elems {
            my $y = 0;
            for ^$!level.sprites[$x].elems {
                self.add_sprite($!level.sprites[$x][$y].Str, $x * TILESIZE, $y * TILESIZE);
                $y++;
            }
            $x++;
        }
    }

    method create {
        self.load_bitmap(GRASS.Str, 'img/grass.png');
        self.load_bitmap(BRICK.Str, 'img/brick.png');
        self.load_bitmap(SKY.Str, 'img/sky.png');
        self.load_spritesheet('mario', 'img/smallmario.png', 38, 49, 4);

        self.load_level('level');
        self.draw_level;

        $!mario = self.add_sprite('mario', 0, 0, as => sub { Mario.new(|%_) });
        $!mario.move(self.width div 2, self.height div 2);
    }

    method keypressed($k) {
        exit if $k eq "Escape";
    }

    method update($dt) {
        my ($dx, $dy) = (0, 0);
        if self.is_pressed("Left") {
            $dx -= 15;
        } elsif self.is_pressed("Right") {
            $dx += 15;
        } elsif self.is_pressed("Up") {
            $dy -= 15;
        } elsif self.is_pressed("Down") {
            $dy += 15;
        }
        if any($dx, $dy) {
            $!mario.move($dx, $dy);
            $!mario.report;
        }
    }
}

given Steroids::Game.new {
    .add_state('main', { Main.new });
    .change_state('main');
    .start;
}
